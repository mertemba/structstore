# Maintainer: Max Mertens <max.mail@dameweb.de>

pkgbase=structstore
pkgname=(structstore structstore_py)
pkgver=0.2.0
pkgrel=1
pkgdesc='Structured object storage, dynamically typed, to be shared between processes'
arch=('x86_64')
url='https://github.com/mertemba/structstore'
license=('BSD-3-Clause')
depends=('glibc' 'gcc-libs' 'yaml-cpp')
structstore_py_depends=('python' 'structstore')
makedepends=('cmake' 'ninja' 'gcc' 'yaml-cpp' 'python' 'python-pip')
source=("git+https://github.com/mertemba/structstore.git")
sha256sums=('SKIP')
options=(!debug)

pkgver() {
    cd "$pkgbase"
    # cutting off 'v' prefix that presents in the git tag
    git describe --long --tags --abbrev=7 --always | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "${srcdir}/${pkgbase}"
    venvdir="${srcdir}/venv"
    python -m venv "${venvdir}"
    source "${venvdir}/bin/activate"
    pip install -r requirements.txt
    cmake -B build -S "." \
        -G Ninja \
        -DBUILD_WITH_PYTHON=ON \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -Wno-dev
    cmake --build build
}

check() {
    # ctest --test-dir build --output-on-failure
    true
}

package_structstore() {
    cd "${srcdir}/${pkgbase}"
    venvdir="${srcdir}/venv"
    source "${venvdir}/bin/activate"
    cmake -B build -S "." \
        -DCMAKE_INSTALL_MESSAGE=NEVER \
        -DBUILD_WITH_PYTHON=OFF
    DESTDIR="$pkgdir" cmake --install build
}

package_structstore_py() {
    cd "${srcdir}/${pkgbase}"
    venvdir="${srcdir}/venv"
    source "${venvdir}/bin/activate"
    cmake -B build -S "." \
        -DCMAKE_INSTALL_MESSAGE=NEVER \
        -DBUILD_WITH_PYTHON=ON
    DESTDIR="$pkgdir" cmake --install build

    # keep only files that are not already included in base package
    find "$pkgdir" -type f | sed "s@$pkgdir@@" | grep -v "$pkgname" \
        | while IFS='' read f; do rm "$pkgdir/$f"; done
    find "$pkgdir" -type d -empty -delete
}
